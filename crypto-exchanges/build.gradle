description = 'BX-bot Exchange Adapters'

dependencies {

    compile project(':crypto-trading-api')
    compile project(':crypto-exchange-api')

    compile libraries.spring_boot_starter
    compile libraries.spring_boot_starter_log4j2
    compile libraries.google_gson
    compile libraries.google_guava
    compile libraries.javax_xml_api
    compile libraries.javax_xml_impl

    testCompile libraries.junit
    testCompile libraries.powermock_junit
    testCompile libraries.powermock_api_easymock
    testCompile libraries.easymock
}

sourceSets {
    integrationTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output

        java.srcDir 'src/integration-test/java'
        resources.srcDir 'src/integration-test/resources'
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTests(type: Test) {

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    testLogging {
        events "passed", "skipped", "failed"
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            // Ignore classes tested using Powermock. JaCoCo won't/can't instrument them:
            // "... right now there is NO WAY TO USE PowerMock with JaCoCo On-the-fly instrumentation."
            // See https://github.com/powermock/powermock/wiki/Code-coverage-with-JaCoCo
            excludes = [
                    'com.gazbert.crypto.exchanges.BitfinexExchangeAdapter*',
                    'com.gazbert.crypto.exchanges.BitstampExchangeAdapter*',
                    'com.gazbert.crypto.exchanges.GdaxExchangeAdapter*',
                    'com.gazbert.crypto.exchanges.GeminiExchangeAdapter*',
                    'com.gazbert.crypto.exchanges.ItBitExchangeAdapter*',
                    'com.gazbert.crypto.exchanges.KrakenExchangeAdapter*',
                    'com.gazbert.crypto.exchanges.OkCoinExchangeAdapter*',
                    'com.gazbert.crypto.exchanges.TestExchangeAdapter*',
                    'com.gazbert.crypto.exchanges.AbstractExchangeAdapter*',
            ]
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.7
            }
        }
    }
}